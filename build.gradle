plugins {
    id 'scala'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.0'
}

group 'decaf.tools'
version '2019.1.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.scala-lang', name: 'scala-library', version: '2.12.0'
    compile group: 'org.scala-lang.modules', name: 'scala-parser-combinators_2.12', version: '1.0.5'
    compile gradleApi()

    testCompile group: 'org.scalatest', name: 'scalatest_2.12', version: '3.0.1'
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

task assembly(type: Jar) {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': 'decaf.tools.ll1pg.Main'
    }

    exclude 'decaf.tools.ll1pg.gradle'
    exclude 'META-INF/gradle-plugins/**'

    from {
        configurations.compile.filter {
            it.path.contains("scala")
        }.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

pluginBundle {
    website = 'https://github.com/paulzfm/ll1pg'
    description = 'A gradle plugin to apply ll1pg'
    vcsUrl = 'https://github.com/paulzfm/ll1pg'
    tags = ['parser generator', 'LL']

    plugins {
        LL1pgPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Gradle plugin for ll1pg'
        }
    }
}

gradlePlugin {
    plugins {
        LL1pgPlugin {
            id = 'decaf.tools.ll1pg'
            implementationClass = 'decaf.tools.ll1pg.gradle.LL1pgPlugin'
        }
    }
}
